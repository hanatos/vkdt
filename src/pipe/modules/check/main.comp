#version 460
#extension GL_GOOGLE_include_directive    : enable

#include "shared.glsl"
#include "colourspaces.glsl"

layout(local_size_x = DT_LOCAL_SIZE_X, local_size_y = DT_LOCAL_SIZE_Y, local_size_z = 1) in;

layout(std140, set = 0, binding = 1) uniform params_t
{
  float thrs;
  int mode;   // 0 off 1 gamut 2 exposure 3 both
  int gamut;  // 0 off 1 spectral locus 2 rec2020 3 rec709
  int chan;   // bits for red green blue
} params;

layout(set = 1, binding = 0) uniform sampler2D img_in;
layout(set = 1, binding = 1) uniform writeonly image2D img_out;

void
main()
{
  ivec2 ipos = ivec2(gl_GlobalInvocationID);
  if(any(greaterThanEqual(ipos, imageSize(img_out)))) return;

  vec3 rgb = texelFetch(img_in, ipos, 0).rgb;

  vec3 rgbc = rgb;
  if(params.gamut == 3)
  { // if rec709 mode: convert via matrix
    const mat3 M = matrix_rec2020_to_rec709;
    rgbc = M * rgb;
  }

  if((params.mode & 1) > 0 && (params.gamut == 2 || params.gamut == 3))
  { // check rec2020/rec709 gamut boundaries:
    if(any(lessThan(rgbc, vec3(params.thrs))))
      rgb = ((ipos.x / 10 + ipos.y / 10) & 1) == 0 ? vec3(1, 0, 1) : vec3(1, 1, 1);
  }
  else if((params.mode & 1) > 0 && (params.gamut == 1))
  {
    vec3 xyY = rec2020_to_xyY(rgb);
    if(outside_spectral_locus(xyY.xy))
      rgb = ((ipos.x / 10 + ipos.y / 10) & 1) == 0 ? vec3(1, 0, 1) : vec3(1, 1, 1);
  }

  if((params.mode & 2) > 0)
  {
    if(all(lessThan(rgb, vec3(params.thrs))))
      rgb = ((ipos.x / 10 + ipos.y / 10) & 1) == 0 ? vec3(1, 1, 0) : vec3(0, 0, 0);
    else if(all(greaterThan(rgb, vec3(1.0-params.thrs))))
      rgb = ((ipos.x / 10 + ipos.y / 10) & 1) == 0 ? vec3(0, 1, 1) : vec3(1, 1, 1);
  }

  if((params.chan & 1) == 0) rgb.r = 0.0;
  if((params.chan & 2) == 0) rgb.g = 0.0;
  if((params.chan & 4) == 0) rgb.b = 0.0;

  imageStore(img_out, ipos, vec4(rgb, 1));
}


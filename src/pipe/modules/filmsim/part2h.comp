#version 460
#extension GL_GOOGLE_include_directive    : enable
#extension GL_EXT_nonuniform_qualifier    : enable
#include "head.glsl"
layout(set = 1, binding = 4) uniform sampler2D img_hal;
layout(push_constant, std140) uniform push_t
{
  float scale;
} push;

void main()
{
  ivec2 ipos = ivec2(gl_GlobalInvocationID);
  if(any(greaterThanEqual(ipos, imageSize(img_out)))) return;
  // load halation + raw exposed film
  vec3 raw = texture(img_in,  (ipos+0.5)/vec2(imageSize(img_out))).rgb;
  vec3 hal = texture(img_hal, (ipos+0.5)/vec2(imageSize(img_out))).rgb;
  vec3 hs = params.halation_scale*params.halation_strength.rgb;
  // mid-tone protection, based on blurred image (hal) for smoother blend
  float p = 3.0*params.halation_midtones;
  float x = max(0.0, dot(hal, vec3(1))/dot(params.halation_strength.rgb, vec3(1)));
  float prot = exp(-p/(1e-3+0.01*x*x));
  hs *= prot;
  const float one_log10 = 0.43429448190325176;
  vec3 log_raw = log((raw+hs*hal)/(1.0+hs))*one_log10;
  vec3 density_cmy = develop_film(log_raw, params.film, ipos, push.scale);
  if(params.process == 0)
  {
    log_raw = enlarger_expose_film_to_paper(density_cmy);
    density_cmy = develop_print(log_raw);
  }
  vec3 rgb = scan(density_cmy);
  imageStore(img_out, ipos, vec4(rgb, 1));
}

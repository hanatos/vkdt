// LDAP's merian histogram code stolen:
#version 460
#extension GL_GOOGLE_include_directive    : enable
#extension GL_EXT_nonuniform_qualifier    : enable
#include "shared.glsl"
#include "colourspaces.glsl"
layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(set = 1, binding = 0) uniform sampler2D img_src;
layout(set = 1, binding = 1, std430) buffer buf_hist { uint  histogram[]; };
layout(std140, set = 0, binding = 1) uniform params_t {
  vec4 abcdr[8];
  vec4 abcdg[8];
  vec4 abcdb[8];
  vec4 xr0, xr1;
  vec4 xg0, xg1;
  vec4 xb0, xb1;
  vec4 yr0, yr1;
  vec4 yg0, yg1;
  vec4 yb0, yb1;
  int  cntr, cntg, cntb;
  int  sel;
  int  mode;
  int  chan;
  int  ychchan;
  int  edit;
  int  pad0, pad1;
  float ddr0, ddrn;
  float ddg0, ddgn;
  float ddb0, ddbn;
  vec4 vtx1[3], vtx2[3], vtx3[3], vtx4[3], vtx5[3], vtx6[3], vtx7[3], vtx8[3];
  vec4 abcd1[6], abcd2[6], abcd3[6], abcd4[6], abcd5[6], abcd6[6], abcd7[6], abcd8[6];
} params;
#include "eq.glsl"
#include "spline.glsl"
const uint hist_size = gl_WorkGroupSize.x * gl_WorkGroupSize.y;
shared uint[hist_size] local_hist;
shared uint count;

uint get_bin(vec3 rgb)
{
  float l = 0.0;
  if(params.mode == 2)
  { // ych
    // vec3 ych = rgb2ych(rgb);
    vec3 ych = rec2020_to_oklsh(rgb);
    l = ych.x; // luma
    if(params.ychchan == 1 || params.ychchan == 3 || params.ychchan == 8)
      l = ych.y; // depend on chroma
    if(params.ychchan == 2 || params.ychchan == 6 || params.ychchan == 7)
      l = ych.z; // depend on hue
    if(params.ychchan == 0) l = l2e(l);
  }
  else
  {
    if     (params.chan == 0) l = rgb.r;
    else if(params.chan == 1) l = rgb.g;
    else if(params.chan == 2) l = rgb.b;
    // else if(params.chan == 0) l = luminance_rec2020(rgb);
    l = l2e(l);
  }
  uint bin = uint(hist_size * l + 0.5);
  return clamp(bin, 0, hist_size - 1);
}

void main()
{
  const ivec2 ipos = ivec2(gl_GlobalInvocationID);

  local_hist[gl_LocalInvocationIndex] = 0;
  if (gl_LocalInvocationIndex == 0) count = 0;
  barrier();

  const ivec2 isize = textureSize(img_src, 0);
  if (all(lessThan(ipos, isize)))
  {
    vec3 color = texelFetch(img_src, ipos, 0).rgb;
    const uint bin = get_bin(color);
    if(bin > 0)
    {
      atomicAdd(local_hist[bin], 1);
      atomicAdd(count, 1);
    }
  }
  barrier();

  atomicAdd(histogram[gl_LocalInvocationIndex], local_hist[gl_LocalInvocationIndex]);
  if (gl_LocalInvocationIndex == 0) atomicAdd(histogram[hist_size], count);
}

